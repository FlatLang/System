package "flatlang/system"

import "flatlang/io/StreamReader"
import "flatlang/io/File"
import "flatlang/io/FileWriter"
import "flatlang/io/FileReader"
import "flatlang/time/Time"
import "flatlang/time/CumulativeTimer"
import "flatlang/system/System"

[AutoFinal]
class System {
    external type FILE
    external type thing

    external void nova_gc_collect()

    external int NULL

    external void exit(int code) as externalExit
    external int system(char command[])
    external FILE* getPipe(char command[], exit(Int, String, Bool))
    external bool fgets(char buffer[], int length, FILE* pipe)
    external bool feof(FILE* pipe)

    [Target c] visible static final String TARGET = "C"
    [Target js] visible static final String TARGET = "Javascript"
    [Target java] visible static final String TARGET = "Java"

    visible static final String OS

    visible static final Int WINDOWS = 1
    visible static final Int LINUX = 2
    visible static final Int MAC_OSX = 3

    visible static final Int OS_INT

    [Target c]
    compiler_visible thread_local external_name static ExceptionData exceptionData

    static {
        external c {
            #ifdef _WIN32
                #{OS = "Windows"};
                #{OS_INT = WINDOWS};
            #elif defined(__linux__)
                #{OS = "Linux"};
                #{OS_INT = LINUX};
            #elif defined(__APPLE__)
                #{OS = "Mac OS X"};
                #{OS_INT = MAC_OSX};
            #endif
        }
        external js {
            switch (process.platform) {
                case "win32": {
                    #{OS = "Windows"};
                    #{OS_INT = WINDOWS};
                    break;
                }
                case "linux": {
                    #{OS = "Linux"};
                    #{OS_INT = LINUX};
                    break;
                }
                case "darwin": {
                    #{OS = "Mac OS X"};
                    #{OS_INT = MAC_OSX};
                    break;
                }
            }
        }
    }

    visible static CumulativeTimer overheadTimer = new CumulativeTimer()

    [Target c]
    public static exit(Int code) {
        externalExit(code)
    }

    [Target java]
    public static exit(Int code) {
        external {
            System.exit(#{code});
        }
    }

    [Target js]
    public static exit(Int code) {
        external {
            process.exit(#{code});
        }
    }

    public static exit(Int code, String message) {
        exit(code, message, false)
    }

    public static exit(Int code, String message, Bool log) {
        if (log) {
            let f = new FileWriter("Log" + Time.currentTimeMillis + ".txt")

            if (f.create()) {
                f.writeLine(message)
            }
        }

        Console.writeLine(message)

        exit(code)
    }

    [Target c]
    public static execute(String command) -> Process {
        FILE* pipe = getPipe(command.chars.data, exit)

        if (pipe == 0) {
            exit(1, "Unable to open pipe")
        }

        return new Process(new FileReader(pipe))
    }

    [Target c]
    public static execute(String[] arguments) =>
        execute(arguments.map(arg => {
            arg.contains(/[ &]/) ? "\"#arg\"" : arg
        }).join(" "))

    compiler_visible static runMain(Int argc, native Char[][] argvs, mainFunc(Array<String>), initialize() = {}, callStaticBlocks() = {}) -> Int => 0 {
        external c {
            #ifdef _WIN32
                setProgramName(#{argvs[0]});
                //signal(SIGSEGV, nova_signal_handler);
                SetUnhandledExceptionFilter(nova_exception_handler);
            #endif

            srand(currentTimeMillis());
        }

        external c {
            nova_garbageData = NOVA_MALLOC(sizeof(void*));
            nova_null = #{new Null()};

            if ((errno = nova_create_semaphore()) != 0) return errno;
        }

        var String[] args

        initialize()

        try {
            callStaticBlocks()

            args = new String[argc]

            for (i in 0..argc) {
                var native Char[] allocated

                external c {
                    #{allocated} = (char*)NOVA_MALLOC(sizeof(char) * strlen(#{argvs[i]}) + 1);
                    copy_string(#{allocated}, #{argvs[i]});
                }

                args[i] = new String(allocated)
            }

            mainFunc(args)
        } catch (Exception e) {
            var message = "#e.class.location in main Thread"

            if (e.message) {
                message += ": #e.message"
            }

            Console.log(message)
            Console.waitForEnter()
        }

        // Thread.ACTIVE_THREADS.forEach({ _.join() })

        external c {
            if ((errno = nova_close_semaphore()) != 0) return errno;
        }

        external c {
            #ifdef _WIN32
                FreeConsole();
            #endif

            NOVA_FREE((void*)#{args});
        }

        nova_gc_collect()
    }

    [Target js]
    public static jsCreateInstance<T>(String className, Int overloadId = -1) -> T {
        var T instance = null

        var String constructorName = "new#className"

        if (overloadId >= 0) {
            constructorName += overloadId
        }

        external {
            #{instance} = novaConstructors[#{constructorName.chars.data}]();
        }

        return instance
    }
}